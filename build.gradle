
plugins {
    id 'java-library'
}
apply plugin: 'java'
apply plugin: 'jacoco'

dependencies {
    testImplementation 'junit:junit:4.12'
}

repositories {
    jcenter()
}

version = '2.0.0'

jar {
    manifest {
        attributes('Implementation-Title': 'Java Nats',
                   'Implementation-Version': '2.0.0')
    }
    exclude("io/nats/examples/**")
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','src/examples/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

task generateAPIDoc(type: Javadoc) {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "NATS.IO Java API"
    excludes = ['io/nats/client/impl', 'io/nats/benchmark']
    classpath = sourceSets.main.runtimeClasspath
    doLast {
            exec {
                println "Updating favicon on all html files"
                workingDir 'build/docs/javadoc'
                // Only on linux, mac at this point
                commandLine 'find', '.', '-name', '*.html', '-exec', 'sed', '-i', '-e', 's#<head>#<head><link rel="icon" type="image/ico" href="favicon.ico">#', '{}', ';'
            }
            copy {
                println "Copying images to javadoc folder"
                from 'src/main/javadoc/images'
                into 'build/docs/javadoc'
            }
        }
}

task examplesJar(type: Jar) {
    baseName 'java-nats-examples'
    manifest {
        attributes('Implementation-Title': 'Java Nats Examples',
                   'Implementation-Version': '2.0.0')
    }
    from(sourceSets.main.output) {
        include "io/nats/examples/**"
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}