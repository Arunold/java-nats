
plugins {
    id 'java-library'
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

version = '2.0.0'

repositories {
    jcenter()
}

configurations {
    examples.extendsFrom implementation
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','src/examples/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': 'Java Nats',
                   'Implementation-Version': '2.0.0',
                   'Implementation-Vendor': 'nats.io')
    }
    exclude("io/nats/examples/**")
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "NATS.IO Java API"
    excludes = ['io/nats/client/impl', 'io/nats/examples']
    classpath = sourceSets.main.runtimeClasspath
    doLast {
            exec {
                println "Updating favicon on all html files"
                workingDir 'build/docs/javadoc'
                // Only on linux, mac at this point
                commandLine 'find', '.', '-name', '*.html', '-exec', 'sed', '-i', '-e', 's#<head>#<head><link rel="icon" type="image/ico" href="favicon.ico">#', '{}', ';'
            }
            copy {
                println "Copying images to javadoc folder"
                from 'src/main/javadoc/images'
                into 'build/docs/javadoc'
            }
        }
}

task examplesJar(type: Jar) {
    baseName 'java-nats-examples'
    manifest {
        attributes('Implementation-Title': 'Java Nats Examples',
                   'Implementation-Version': '2.0.0',
                   'Implementation-Vendor': 'nats.io')
    }
    from(sourceSets.main.output) {
        include "io/nats/examples/**"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.nats'
            artifactId 'jnats'
            version '2.0'
            pom {
                name = 'NATS Java Client'
                description = 'A Java 8 client for the NATS server.'
                url = 'https://github.com/nats-io/java-nats'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/nats-io/java-nats.git'
                    url = 'https://github.com/nats-io/java-nats'
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url project.hasProperty("release") ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}
