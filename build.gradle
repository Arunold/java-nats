
plugins {
    id 'java-library'
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

version = '2.0.0'
archivesBaseName = 'jnats'
group = 'io.nats'

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','src/examples/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': 'Java Nats',
                   'Implementation-Version': '2.0.0',
                   'Implementation-Vendor': 'nats.io')
    }
    exclude("io/nats/examples/**")
}

test {
    maxHeapSize = "2g"
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        jvmArgs '-Djava.security.egd=file:/dev/./urandom'
    }
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "NATS.IO Java API"
    excludes = ['io/nats/client/impl', 'io/nats/examples']
    classpath = sourceSets.main.runtimeClasspath
    doLast {
            exec {
                println "Updating favicon on all html files"
                workingDir 'build/docs/javadoc'
                // Only on linux, mac at this point
                commandLine 'find', '.', '-name', '*.html', '-exec', 'sed', '-i', '-e', 's#<head>#<head><link rel="icon" type="image/ico" href="favicon.ico">#', '{}', ';'
            }
            copy {
                println "Copying images to javadoc folder"
                from 'src/main/javadoc/images'
                into 'build/docs/javadoc'
            }
        }
}

task examplesJar(type: Jar) {
    baseName 'java-nats-examples'
    manifest {
        attributes('Implementation-Title': 'Java Nats Examples',
                   'Implementation-Version': '2.0.0',
                   'Implementation-Vendor': 'nats.io')
    }
    from(sourceSets.main.output) {
        include "io/nats/examples/**"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate { // only report on main library not examples
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/examples**'])
        })
    }
}

task generatePom{
    doLast {
        pom {
            project {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/java-nats'
                }
            }
        }.writeTo("$buildDir/pom.xml")
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

/*signing {
    sign configurations.archives
} */

uploadArchives {
    repositories {
        mavenDeployer {
            
            repository(url: "file://$buildDir/repos")

            /*repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }*/

            pom.project {
                name 'NATS Java Client'
                packaging 'jar'
                description 'Client library for working with the NATS messaging system.'
                url 'http://nats.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/java-nats'
                }
            }
        }
    }
}

